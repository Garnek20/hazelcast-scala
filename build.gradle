buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.jfrog.bintray' version "1.7"
    id 'scala'
    id 'maven-publish'
}

ext {
    vJUnit = '4.12'
    vScalaTest = '3.0.0-RC4' //'2.2.6'
    vScala = '2.11'
    vScalaFull = "$vScala.8"
    vHazelcast = '3.7.+'
    vKryo = '3.0.+'
    vObjenesis = '2.+'
    vLz4 = '1.3.0'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
if (version == '') {
    version = 'SNAPSHOT'
}
if (version == 'SNAPSHOT') {
    version = tsVersion(version)
}

def tsVersion(version) {
    java.text.SimpleDateFormat tsFmt = new java.text.SimpleDateFormat('yyMMddHHmm')
    tsFmt.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')))
    return "${version}-b${tsFmt.format(new Date())}"
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:$vJUnit"
    testCompile "org.scala-lang:scala-compiler:$vScalaFull"
    testCompile "org.scalatest:scalatest_$vScala:$vScalaTest"

    compile "org.scala-lang:scala-reflect:$vScalaFull"

    compile "com.hazelcast:hazelcast:$vHazelcast"
    compile "com.hazelcast:hazelcast-client:$vHazelcast"

    compile "javax.cache:cache-api:1.0.0"
    compile "javax.transaction:transaction-api:1.1"

    compile "com.esotericsoftware:kryo:$vKryo"
    compile "org.objenesis:objenesis:$vObjenesis"

    compile "net.jpountz.lz4:lz4:$vLz4"

}

jar {
    baseName = "${project.name}_$vScala"
    manifest {
        attributes "Implementation-Title": project.name, "Implementation-Version": version
    }
}

task sourceJar(type: Jar) {
    baseName "${project.name}_$vScala"
    classifier "sources"
    from sourceSets.main.allScala
}
task docsJar(type: Jar, dependsOn: scaladoc) {
    baseName "${project.name}_$vScala"
    classifier "docs"
    from scaladoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.hazelcast'
            artifactId jar.baseName

            from components.java
            artifact sourceJar
            artifact docsJar
            pom.withXml { xml ->
                def includeArtifacts = ['cache-api', 'transaction-api']
                xml.asNode().dependencies.dependency.findAll{ ! includeArtifacts.contains(it.artifactId.text()) }.each { dep ->
                    dep.scope[0].value = "provided"
                }
            }
        }
    }
}

bintray {
    user = System.properties.bintrayUser ?: "n/a"
    key = System.properties.bintrayApiKey ?: "n/a"
    dryRun = (user == "n/a" || key == "n/a" || version.startsWith('SNAPSHOT'))
    publish = !dryRun
    publications = ['maven']
    pkg {
        repo = System.properties.bintrayRepo
        name = System.properties.bintrayName
    }
}
